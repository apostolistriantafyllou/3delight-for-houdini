/*
	OSL does not allow initializing a variable-sized array with an emtpy
	array value. As a workaround, the AOV variable name arrays default value 
	is set to a single empty string,. which is ignored in the shader. The AOV
	value arrays default value is set to a single value for the same reason.
*/
shader dlAOVGroup(
	color colorAOVValues[] = { color( 0, 0, 0) },
	string colorAOVNames[] = { "" },
	output closure color outColor = color( 0, 0, 0 ) )
{
	if( !raytype("camera") )
	{
		return;
	}
	
	int numValues = arraylength( colorAOVValues );
	int numNames = arraylength( colorAOVNames );
	if( numValues == numNames )
	{
		for( int i = 0; i < numValues; i++ )
		{
			if( colorAOVNames[i] != "" )
			{
				outColor += debug( colorAOVNames[i] ) * colorAOVValues[i];
			}
		}
	}
}
